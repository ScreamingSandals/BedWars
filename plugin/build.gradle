apply plugin: "com.github.gmazzo.buildconfig"

ScreamingLibBuilder()
        .platforms('bukkit')
        .modules('core', 'cloud', 'hologram', 'placeholders', 'sidebar', 'healthindicator', 'npc', 'signs', 'economy')
        .version(SCREAMING_LIB_VERSION)
        .simpleInventories(SIMPLE_INVENTORIES_VERSION)
        .universalModules('lang')
        .enableAnnotationProcessor()
        .build()

dependencies {
    /* PROVIDED */
    compileOnly paperLegacy(PAPER_VERSION)
    compileOnly 'com.onarandombox.multiversecore:Multiverse-Core:' + MULTIVERSE_VERSION
    compileOnly('me.ebonjaeger:perworldinventory-kt:' + PERWORLDINVENTORY_KT_VERSION)  {
        exclude group: '*', module: '*'
    }
    compileOnly('me.gnat008:perworldinventory:' + PERWORLDINVENTORY_VERSION) {
        exclude group: '*', module: '*'
    }
    compileOnly 'com.alessiodp.parties:parties-api:' + PARTIES_VERSION

    /* SHADED */
    implementation project(':BedWars-API')
    implementation project(':BedWars-NMS')
    implementation 'org.screamingsandals.language.bedwars:BedWarsLanguage:' + (BEDWARS_VERSION =~ /^\d+.\d+/).findAll()?.first() + '-SNAPSHOT'
    implementation 'com.zaxxer:HikariCP:' + HIKARI_VERSION
    implementation 'org.screamingsandals.lib:cloud-extras:' + SCREAMING_LIB_VERSION
    implementation bStats(BSTATS_VERSION)
    implementation 'me.lucko:commodore:' + COMMODORE_VERSION, {
        exclude group: 'com.mojang', module: 'brigadier'
    }
}

buildConfig {
    className('VersionInfo')
    packageName('org.screamingsandals.bedwars')

    buildConfigField('String', 'NAME', "\"${project.name}\"")
    buildConfigField('String', 'VERSION', "\"${project.version}\"")
    buildConfigField('String', 'BUILD_NUMBER', "\"${System.getenv('BUILD_NUMBER') ?: "custom"}\"")
}

prepareTestTask()
    .versions('1.18.2', '1.17.1', '1.16.5', '1.15.2', '1.14.4', '1.13.2', '1.12.2', '1.11.2', '1.10.2', '1.9.4')
    .setSubdirectory('paper-master')
    .jvmArgs('-Dio.papermc.paper.suppress.sout.nags=true', '-DPaper.IgnoreJavaVersion=true') // suppress System.out.println nag
    .onlineMode(false)
    .pluginJar(shadowJar.archiveFile.get().asFile.toPath())
    .build()

repositories {
    mavenCentral()
}

discord {
    title = "BedWars Master #${System.getenv('BUILD_NUMBER') ?: "custom"}"
    content = 'New build of BedWars is now available!'
    url = System.getenv('WEBHOOK_URL')
    allowedClassifiersAndExtensions = ['.jar']

    if (System.getenv('GITHUB_ACTIONS_URL')) {
        buildInformationUrl = System.getenv('GITHUB_ACTIONS_URL')
    }
    /*if (System.getenv('GITHUB_COMMIT_MESSAGE')) {
        content = "New build of BedWars is now available!\n\n```\n${System.getenv('GITHUB_COMMIT_MESSAGE')}\n```"
    }*/

    registerTask(project)
}