dependencies {
    /* PROVIDED */
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'com.onarandombox.multiversecore:Multiverse-Core:4.0.0'
    compileOnly 'me.confuser:BarAPI:3.5'
    compileOnly 'me.clip:placeholderapi:2.9.2'
    compileOnly('me.ebonjaeger:perworldinventory-kt:2.3.1')  {
        exclude group: '*', module: '*'
    }
    compileOnly('me.gnat008:perworldinventory:1.11.5') {
        exclude group: '*', module: '*'
    }
    compileOnly 'io.netty:netty-all:4.1.42.Final'
    compileOnly 'net.citizensnpcs:citizens-main:2.0.27-SNAPSHOT'
    compileOnly 'com.alessiodp.parties:parties-api:2.6.16'

    /* SHADED */
    shade project(':BedWars-API')
    shade 'com.zaxxer:HikariCP:3.3.0'
    shade 'org.screamingsandals.simpleinventories:SimpleInventories-Core:1.0.6.5'
    shade 'org.bstats:bstats-bukkit:1.7'
    shade 'pronze.lib:scoreboard:1.0.0'
    shade paperlib()

}

task generateBuildInfoFile {
    def outputDir = new File(projectDir, "build/generated/buildinfo")
    doFirst {
        mkdir outputDir

        new File(outputDir, "build_info.yml").withWriter { writer ->
            def contents = "build: "
            if (System.getenv('BUILD_NUMBER') != null) {
                contents += System.getenv('BUILD_NUMBER')
            } else {
                contents += "custom"
            }
            writer.write(contents)
        }
    }
}

sourceSets {
    main {
        output.dir(new File(projectDir, "build/generated/buildinfo"), builtBy: 'generateBuildInfoFile')
    }
}

enableSpigradleSpigot()

spigot {
    name = 'BedWars'
    authors = ['ScreamingSandals <Misat11, Ceph, Pronze>']
    apiVersion = '1.13'
    load = POST_WORLD
    softDepends = [
            'Vault',
            'Multiverse-Core',
            'Multiworld',
            'MultiWorld',
            'UltimateCore',
            'PlaceholderAPI',
            'BarAPI',
            'PerWorldInventory',
            'SlimeWorldManager',
            'My_Worlds',
            'Citizens',
            'Parties'
    ]
    commands {
        bw {
            aliases = ['bedwars']
            description = 'Command of BW plugin'
            usage = '/bw [subcommand]'
        }
    }
}