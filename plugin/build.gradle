import org.apache.tools.ant.filters.*

apply plugin: "com.github.gmazzo.buildconfig"

dependencies {
    /* PROVIDED */
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'com.onarandombox.multiversecore:Multiverse-Core:4.0.0'
    compileOnly 'me.clip:placeholderapi:2.9.2'
    compileOnly('me.ebonjaeger:perworldinventory-kt:2.3.1')  {
        exclude group: '*', module: '*'
    }
    compileOnly('me.gnat008:perworldinventory:1.11.5') {
        exclude group: '*', module: '*'
    }
    compileOnly 'io.netty:netty-all:4.1.79.Final'
    compileOnly 'net.citizensnpcs:citizens-main:2.0.30-SNAPSHOT'
    compileOnly 'com.alessiodp.parties:parties-api:3.2.6'
    compileOnly 'com.viaversion:viaversion-api:4.4.1'

    /* SHADED */
    implementation project(':BedWars-API')
    implementation project(':BedWars-NMS')
    implementation 'com.zaxxer:HikariCP:3.3.0'
    implementation 'org.screamingsandals.simpleinventories:SimpleInventories-Core:1.0.6.12'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
}

processResources {
    filter ReplaceTokens, tokens: [
            "version": project.version
    ]
}

jar {
    manifest {
        attributes([
                'paperweight-mappings-namespace': 'mojang'
        ])
    }
}

buildConfig {
    className('VersionInfo')
    packageName('org.screamingsandals.bedwars')

    buildConfigField('String', 'NAME', "\"${project.name}\"")
    buildConfigField('String', 'VERSION', "\"${project.version}\"")
    buildConfigField('String', 'BUILD_NUMBER', "\"branch-0.2.x/${System.getenv('BUILD_NUMBER') ?: "custom"}\"")
}

prepareTestTask()
        .versions('1.20.6', '1.20.4', '1.20.2', '1.20.1', '1.19.4', '1.19.3', '1.18.2', '1.17.1', '1.16.5', '1.15.2', '1.14.4', '1.13.2', '1.12.2', '1.11.2', '1.10.2', '1.9.4', '1.8.8')
        .setSubdirectory("paper-0-2-x-branch")
        .jvmArgs('-Dio.papermc.paper.suppress.sout.nags=true', '-DPaper.IgnoreJavaVersion=true') // suppress System.out.println nag
        .onlineMode(false)
        .pluginJar(shadowJar.archiveFile.get().asFile.toPath())
        .addToServerProperties('level-type', 'flat')
        .build()

discord {
    title = "BedWars ver/0.2.x #${System.getenv('BUILD_NUMBER') ?: "custom"}"
    content = 'New build of BedWars is now available!'
    url = System.getenv('WEBHOOK_URL')
    allowedClassifiersAndExtensions = ['.jar']

    if (System.getenv('GITHUB_ACTIONS_URL')) {
        buildInformationUrl = System.getenv('GITHUB_ACTIONS_URL')
    }

    registerTask(project)
}
