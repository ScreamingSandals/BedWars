plugins {
    id 'org.screamingsandals.plugin-builder' version '1.0.65' apply false
    id 'org.screamingsandals.nms-mapper' version '1.1.0' apply false
    id 'com.github.gmazzo.buildconfig' version '3.0.0' apply false
}

allprojects {
    group = 'org.screamingsandals.bedwars'
    version = '0.2.22'
}

if (version.toString().endsWith('-SNAPSHOT')) {
    defaultTasks 'clean', 'screamCompile'
} else {
    defaultTasks 'clean', 'screamCompile', ':BedWars-API:allowJavadocUpload'
}

subprojects {
    System.setProperty("JavadocUploadCustomDirectoryPath", "sbw-0-2-x")
    apply plugin: 'java'
    apply plugin: 'org.screamingsandals.plugin-builder'

    repositories {
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'  }
        maven { url 'https://repo.onarandombox.com/content/groups/public'  }
        maven { url 'https://ci.frostcast.net/plugin/repository/everything'  }
        maven { url 'https://repo.citizensnpcs.co'  }
        maven { url 'https://repo.codemc.org/repository/maven-public/'  }
        maven { url 'https://jitpack.io'  }
        maven { url 'https://repo.alessiodp.com/releases/'  }
    }

    dependencies {
        compileOnly paperLegacy("1.16.5")
    }

    enableShadowPlugin()

    shadowJar {
        relocate 'com.zaxxer', 'org.screamingsandals.bedwars.lib.HikariCP'
        relocate 'org.screamingsandals.simpleinventories', 'org.screamingsandals.bedwars.lib.sgui'
        relocate 'org.bstats', 'org.screamingsandals.bedwars.lib.bstats'

        if (System.getenv('BUILD_NUMBER') != null) {
            archiveClassifier.set(System.getenv('BUILD_NUMBER'))
        } else {
            archiveClassifier.set('dev')
        }
    }

    sourceCompatibility = '1.8'
}
