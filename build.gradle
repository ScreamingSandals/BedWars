plugins {
    id 'org.screamingsandals.plugin-builder' version "${SCREAMING_PLUGIN_BUILDER_VERSION}" apply false
    id 'org.screamingsandals.plugin-slib' version "${SCREAMING_PLUGIN_BUILDER_VERSION}" apply false
    id 'org.screamingsandals.nms-mapper' version "${NMS_MAPPER_VERSION}" apply false
    id 'com.github.gmazzo.buildconfig' version "${BUILDCONFIG_VERSION}" apply false
}

allprojects {
    group = 'org.screamingsandals.bedwars'
    version = BEDWARS_VERSION
}

if (version.toString().endsWith('-SNAPSHOT')) {
    defaultTasks 'clean', 'screamCompile'
} else {
    defaultTasks 'clean', 'screamCompile', ':BedWars-API:allowJavadocUpload'
}

subprojects {
    apply plugin: 'java'

    if (project.name != 'BedWars-API' && project.name != 'BedWars') { // upload only BW and BW-API
        project.ext.disablePublishingToMaven = true
    }

    apply plugin: 'org.screamingsandals.plugin-builder'

    if (project.name != 'BedWars-NMS') {
        apply plugin: 'org.screamingsandals.plugin-slib'

        slib {
            version(SCREAMING_LIB_VERSION)

            multiModulePlatforms('BedWars-%s', 'bukkit')
            multiModuleUniversalSubproject('BedWars') // custom name
            multiModuleApiSubproject('BedWars-API', 'org.screamingsandals.bedwars.api.utils.Wrapper')
            useApiConfigurationInsteadOfImplementation(true)

            additionalContent {
                module('cloud')
                module('hologram')
                module('placeholders')
                module('sidebar')
                module('healthindicator')
                module('npc')
                module('signs')
                module('economy')

                lang()
                singleModule('cloud-extras')

                simpleInventories {
                    version(SIMPLE_INVENTORIES_VERSION)
                }
            }
        }
    }

    enableShadowPlugin()

    configurations.all {
        // Check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'  }
        maven { url 'https://repo.onarandombox.com/content/groups/public'  }
        maven { url 'https://repo.codemc.org/repository/maven-public/'  }
        maven { url 'https://repo.alessiodp.com/releases/'  }
    }

    shadowJar {
        relocate 'com.zaxxer', 'org.screamingsandals.bedwars.lib.HikariCP'
        relocate 'org.bstats', 'org.screamingsandals.bedwars.lib.ext.bstats'
        relocate 'io.papermc.lib', 'org.screamingsandals.bedwars.lib.ext.paperlib'
        relocate 'org.spongepowered.configurate', 'org.screamingsandals.bedwars.lib.ext.configurate'
        relocate 'org.yaml.snakeyaml', 'org.screamingsandals.bedwars.lib.ext.snakeyaml'
        relocate 'com.google.gson', 'org.screamingsandals.bedwars.lib.ext.gson'
        relocate 'com.typesafe.config', 'org.screamingsandals.bedwars.lib.ext.typesafe'
        relocate 'io.leangen.geantyref', 'org.screamingsandals.bedwars.lib.ext.geantyref'
        relocate 'org.checkerframework', 'org.screamingsandals.bedwars.lib.ext.checkerframework'
        relocate 'cloud.commandframework', 'org.screamingsandals.bedwars.lib.ext.cloud'
        relocate 'me.lucko.commodore', 'org.screamingsandals.bedwars.lib.ext.commodore'

        if (System.getenv('BUILD_NUMBER') != null) {
            archiveClassifier.set(System.getenv('BUILD_NUMBER'))
        } else {
            archiveClassifier.set('dev')
        }
    }

    sourceCompatibility = '11.0'
}
